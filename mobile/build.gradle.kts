import org.jetbrains.kotlin.gradle.dsl.JvmTarget
import java.util.Properties

plugins {
    alias(libs.plugins.android.application)
    alias(libs.plugins.kotlin.compose)
    alias(libs.plugins.kotlin.ksp)
    alias(libs.plugins.kotlinx.serialization)
    alias(libs.plugins.dagger.hilt)
    alias(libs.plugins.ktlint)
    id("justjanne.version")
}

android {
    namespace = "de.justjanne.voctotv.mobile"
    compileSdk = 37

    defaultConfig {
        applicationId = "de.justjanne.voctotv"

        minSdk = 26
        targetSdk = 37

        versionCode = versionCode?.times(2)?.plus(1) ?: 1
    }

    buildFeatures {
        buildConfig = true
    }

    data class SigningData(
        val storeFile: String,
        val storePassword: String,
        val keyAlias: String,
        val keyPassword: String,
    )

    fun signingData(properties: Properties?): SigningData? {
        if (properties == null) return null

        val storeFile = properties.getProperty("storeFile") ?: return null
        val storePassword = properties.getProperty("storePassword") ?: return null
        val keyAlias = properties.getProperty("keyAlias") ?: return null
        val keyPassword = properties.getProperty("keyPassword") ?: return null

        return SigningData(storeFile, storePassword, keyAlias, keyPassword)
    }

    fun Project.properties(fileName: String): Provider<Properties> =
        providers
            .fileContents(rootProject.layout.projectDirectory.file(fileName))
            .asBytes
            .map { Properties().apply { load(it.inputStream()) } }

    signingConfigs {
        signingData(rootProject.properties("signing.properties").orNull)?.let {
            register("release") {
                storeFile = file(it.storeFile)
                storePassword = it.storePassword
                keyAlias = it.keyAlias
                keyPassword = it.keyPassword
            }
        }
    }

    buildTypes {
        release {
            isMinifyEnabled = true
            isShrinkResources = true
            proguardFiles(getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro")
            if (signingConfigs.names.contains("release")) {
                signingConfig = signingConfigs.getByName("release")
            }
        }

        debug {
            applicationIdSuffix = ".debug"
        }
    }

    buildFeatures {
        compose = true
    }

    bundle {
        language {
            enableSplit = false
        }
        density {
            enableSplit = true
        }
        abi {
            enableSplit = true
        }
    }
}

kotlin {
    compilerOptions {
        jvmTarget = JvmTarget.JVM_11
    }
}

dependencies {
    implementation(libs.androidx.core.ktx)
    implementation(libs.androidx.appcompat)
    implementation(platform(libs.androidx.compose.bom))
    implementation(libs.androidx.ui)
    implementation(libs.androidx.ui.graphics)
    implementation(libs.androidx.ui.tooling.preview)
    implementation(libs.androidx.lifecycle.runtime.ktx)
    implementation(libs.androidx.activity.compose)
    androidTestImplementation(platform(libs.androidx.compose.bom))
    androidTestImplementation(libs.androidx.ui.test.junit4)
    debugImplementation(libs.androidx.ui.tooling)
    debugImplementation(libs.androidx.ui.test.manifest)

    implementation(libs.androidx.splashscreen)

    implementation(libs.androidx.material)
    implementation(libs.androidx.material3)

    implementation(libs.androidx.paging)

    implementation(libs.androidx.lifecycle.viewmodel.compose)
    implementation(libs.androidx.navigation3.ui)
    implementation(libs.androidx.navigation3.runtime)
    implementation(libs.androidx.lifecycle.viewmodel.navigation3)
    implementation(libs.androidx.hilt.navigation.compose)

    implementation(libs.androidx.media3.exoplayer.core)
    implementation(libs.androidx.media3.cast)
    implementation(libs.androidx.media3.session)
    implementation(libs.androidx.media3.extractor)
    implementation(libs.androidx.media3.compose)
    implementation(libs.androidx.media3.ui)

    implementation(libs.kotlinx.serialization.json)
    implementation(libs.kotlinx.coroutines.core)

    implementation(project(":api"))
    implementation(project(":common"))

    implementation(libs.retrofit.core)
    implementation(libs.retrofit.kotlinx.serialization)

    implementation(libs.coil.compose)
    implementation(libs.coil.svg)
    implementation(libs.coil.network.okhttp)

    implementation(libs.dagger.hilt.android)
    ksp(libs.dagger.hilt.compiler)
}
